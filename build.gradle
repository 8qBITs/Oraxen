import java.text.SimpleDateFormat

plugins {
    id "java"
    id "idea"
    id "eclipse"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

apply plugin: 'maven'

group "com.oraxen"
def pluginVersion = "dev-2.0.0"
version = pluginVersion

repositories {
    mavenCentral()
    mavenLocal()
    //Spigot
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    //Because Spigot depends on Bungeecord ChatComponent-API
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    //JitPack
    maven { url 'https://jitpack.io' }
    //ProtocolLib
    maven { url "https://repo.dmulloy2.net/nexus/repository/public/" }
    //Minecraft repo (commodore)
    maven { url "https://libraries.minecraft.net/" }
    //MythicMobs
    maven { url "https://mvn.lumine.io/repository/maven-public/" }
    //PlaceHolderAPI
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    //EffectLib
    maven { url 'https://maven.elmakers.com/repository/' }
    //sk89q-repo
    maven { url 'https://maven.enginehub.org/repo/' }
    //CodeMc (bstats)
    maven { url "https://repo.codemc.org/repository/maven-public" }
    jcenter() // needed for building shadow from protocolLib 4.5.0
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
    options.encoding = "UTF-8"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

dependencies {
    // The Spigot API with no shadowing. Requires the OSS repo.
    compileOnly group: "org.spigotmc", name: "spigot", version: "1.16.4-R0.1-SNAPSHOT"
    
    // Implement some libs
    compileOnly fileTree(dir: "libs/compile", include: ["*.jar"])
    implementation fileTree(dir: "libs/implement", include: ["*.jar"])
    
    // Implement some dependencies
    implementation group: 'org.pf4j', name: 'pf4j', version: '3.5.0'
    implementation group: 'org.reflections', name: 'reflections', version: '0.9.11'
    implementation group: 'com.syntaxphoenix.syntaxapi', name: 'net', version: '2.0.6'
    implementation group: 'com.syntaxphoenix.syntaxapi', name: 'java', version: '2.0.6'
    implementation group: 'com.syntaxphoenix.syntaxapi', name: 'command', version: '2.0.5'
    implementation group: 'com.syntaxphoenix.syntaxapi', name: 'event', version: '2.0.3'
    implementation group: 'com.syntaxphoenix.syntaxapi', name: 'random', version: '2.0.3'
    implementation group: 'com.syntaxphoenix.syntaxapi', name: 'thread', version: '2.0.3'
    implementation group: 'com.syntaxphoenix.syntaxapi', name: 'logging', version: '2.0.3'
    implementation group: 'com.syntaxphoenix.syntaxapi', name: 'version', version: '2.0.3'
    implementation group: 'com.syntaxphoenix.syntaxapi', name: 'config-yaml', version: '2.0.3'
    implementation group: 'com.syntaxphoenix.syntaxapi', name: 'service-base', version: '2.0.3'
}

processResources {
    filesNotMatching(["**/*.png", "**/models/**", "**/textures/**"]) {
        expand projectVersion: pluginVersion
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

shadowJar {
	pluginVersion = pluginVersion + "-B" + LocalDateTime.now().format("YYYY-DDD-AAAAAAAA");
    minimize {
        exclude("com.oraxen.api.*")
        exclude("com.syntaxphoenix.syntaxapi.*")
    }
    manifest {
        attributes(
                'Built-By': System.properties['user.name'],
                'Version': pluginVersion,
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd' 'HH:mm:ss.SSSZ").format(new Date()),
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

compileJava.dependsOn clean
build.dependsOn shadowJar
